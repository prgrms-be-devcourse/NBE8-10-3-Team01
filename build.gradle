plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'

    id 'org.jetbrains.kotlin.jvm' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.2.21'
    id 'org.jetbrains.kotlin.plugin.allopen' version '2.2.21'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
description = 'plog'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 1. Security & JWT
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // 2. Web & Validation
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // 3. Database (JPA, MySQL, H2, Caffeine Cache)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'
    developmentOnly 'org.springframework.boot:spring-boot-h2console'
    implementation 'io.minio:minio:8.5.7'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'

    // 4. API Documentation (SpringDoc v3)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    // 5. Utility
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    implementation 'org.commonmark:commonmark:0.21.0'

    // 6. Test
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 코틀린 표준 라이브러리 및 리플렉션
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    // Kotlin Data Class를 JSON으로 변환하기 위한 필수 모듈
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll("-Xjsr305=strict", "-Xannotation-default-target=param-property")
    }
}

allOpen {
    annotation("jakarta.persistence.Entity")
    annotation("jakarta.persistence.MappedSuperclass")
    annotation("jakarta.persistence.Embeddable")
}

// .env 로부터 테스트 환경변수 로드
def envProps = new Properties()
file('.env').withInputStream { stream ->
    stream.withReader('UTF-8') { reader ->
        reader.eachLine { line ->
            line = line.trim()
            if (line && !line.startsWith('#') && line.contains('=')) {
                def (k, v) = line.split('=', 2)
                envProps[k] = v
            }
        }
    }
}

tasks.named('test') {
    environment envProps
    useJUnitPlatform()
}